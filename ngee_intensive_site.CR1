'Program name: Z:\Stations\_Programs\STANDARD_SETUP_CR1000.CR1
'Date written: 6/8/2009
'Transform Version: 0.5
'Transform file: C:\Program Files\Campbellsci\LoggerNet\CR10X.TRN Version: 1.1
'
'This program was converted from Z:\Stations\_Programs\STANDARD_SETUP.CSI
'using Campbell Scientific's Transformer application.
'
'This is the standard program with snow depth.
'It was converted and adapted from the CR10X version

'Program for the Western AK sites summer 2012

'\\\\\\\\\\\\\\\\\\\\\\\\\\\\ WIRING ////////////////////////////
'CR1000
'**SE Channel
'1 - Multiplexer H1 - Ref. Resistor to Vx1
'2 -
'3 -
'4 -
'5 - T109_Air (red)
'6 - T109_Hydra 1 (red)
'7 - T109_Hydra 2 (red)
'8 - T109_Hydra 3 (red)
'9 - T109_Deep 1 (red)
'10 - T109_Deep 2 (red)
'11 - T109_Deep 3 (red)
'12 -
'13 - heat flux plate (white)
'14 - heat flux plate (green)
'15 -
'16 -

'**Vx
'1 - Thermistor Probes
'2 - T109's Deep (black)
'3 - T109's Air and Vitel (black)

'**Control Ports
'1 - Multiplexer Reset
'2 - Multiplexer Clk
'3 - Hydra Probes (orange)
'4 -
'5 -
'6 -
'7 - SR50a (grn)
'8 - Power control for Comm Equipment

'**Switched 12V
'Hydra Probes (red)

'**Signal Ground
'SR50 (Wht.)
'T109 (Purple)
'Hydra (Yellow)

'**Multiplexer
'H - Thermistor Probe signal
'L - not used

'**Thermistor Probe on Multiplexer
'Thermistor#	depth (cm)	Pair Color	  Individual Wire Color
'1	          2	          White/Blue	  Blue
'2	          5		                      White
'3	          10	        White/Orange	Orange
'4	          15		                    White
'5	          20	        White/Green	  Green
'6	          25		                    White
'7	          30	        White/Brown	  Brown
'8	          35		                    White
'9	          40	        White/Slate	  Slate
'10	          50		                    White
'11	          60	        Red/Blue	    Blue
'12	          70		                    Red
'13	          80	        Red/Orange	  Orange
'14	          100		                    Red
'15	          125	        Red/Green	    Green
'16	          150		                    Red
'
'Common		                Black/Blue	  Both Black's
'		                      Black/Orange


'\\\\\\\\\\\\\\\\\\\\\\\\\ DECLARATIONS /////////////////////////

Public Vbat
Public TempInt, TempHydra(3), TempAir, TempDeep(3)
Public TempHydra_offset(3), TempAir_offset, TempDeep_offset(3)
Public Hydra(3,9) '9 parameters from the Hydra Probe's II
Public Hydra_moisture(3), Hydra_temp(3), Hydra_cond(3), Hydra_real_perm(3), Hydra_img_perm(3)
Public Raw_Dist, Distance, SnowDepth
Public CommFlag, rtime(9), CommTimer
Public TP1_Rt, TP1_ratio, TP1_temp(16), TP1_offset(16), TP1_num
Public TPCalFlag As Boolean 'FALSE = need to load, TRUE = loaded
Dim i,j,k, FID
Dim TPline As String
Public shf

'Steinhart-Hart Fit Constants
Const C1 = 0.0011281
Const C2 = 0.0002343
Const C3 = 0.000000086946
'Fixed resistor value
Const Rf = 25000

Const SR50_height = 1.58	'height of SR50 snow sensor above the ground
Const CommON = 30        'number of minutes to leave comm equipment on for
Const CommHour = 15.5    'hour of day to turn comm equipment ON (24 hour time)
Const CommDay = 0        'day of week to turn comm equipment ON (1-7; Sunday = 1, 0 for every day)

Const HFP01_cal = 64.9    'multiplier for HFP01, sensitivity (around 60 uV/Wm-2)

'\\\\\\\\\\\\\\\\\\\\\\\\ OUTPUT SECTION ////////////////////////

DataTable(site_data,true,-1)
  DataInterval(0,60,Min,10)
  Average(1, Vbat,FP2, 0)
  Average(1,TempInt,FP2, 0)
  Average(16,TP1_temp(),FP2,False)
  Average(1,TempAir,FP2, 0)
  Average(3,TempHydra(),FP2, 0)
  Average(3,TempDeep(),FP2,0)
  'Hydra Probe's
  Sample(3,Hydra_moisture,FP2)  'soil moisture, VWC
  Sample(3,Hydra_temp,FP2)  'soil temperature, deg. C
  Sample(3,Hydra_cond,FP2)  'soil conductivity, Siemens / Meter
  Sample(3,Hydra_real_perm,FP2)  'real dielectric permittivity
  Sample(3,Hydra_img_perm,FP2)  'imaginary dielectric permittivity
  Average(1,shf,FP2,0)  'soil heat flux plate
  Sample(1,SnowDepth,FP2)
EndTable

'\\\\\\\\\\\\\\\\\\\\\\\\\\\ PROGRAM ////////////////////////////

BeginProg
  'offsets for T109's
  TempAir_offset = -0.070
  TempHydra_offset(1) = 0.017
  TempHydra_offset(2) = -0.016
  TempHydra_offset(3) = -0.047
  TempDeep_offset(1) = -0.034
  TempDeep_offset(2) = -0.007
  TempDeep_offset(3) = 0.016
  'Thermistor Probe offsets
  TP1_offset(1) = 0.04
  TP1_offset(2) = 0.083
  TP1_offset(3) = 0.033
  TP1_offset(4) = 0.061
  TP1_offset(5) = 0.024
  TP1_offset(6) = 0.057
  TP1_offset(7) = 0.012
  TP1_offset(8) = 0.043
  TP1_offset(9) = 0.041
  TP1_offset(10) = -0.008
  TP1_offset(11) = 0.068
  TP1_offset(12) = 0.046
  TP1_offset(13) = 0.015
  TP1_offset(14) = -0.009
  TP1_offset(15) = -0.04
  TP1_offset(16) = 0.048

  Scan(1,Min, 10, 0)
    Battery(Vbat)
    PanelTemp(TempInt,_60Hz)

    'measure thermistor probe(s) on multiplexer
    PortSet(1,1)  'Multiplexer ON
    i = 1
    SubScan(0,uSec,16)
      PulsePort(2,10000)
      'thermistor probe 1
      BrHalf (TP1_ratio,1,mV2500,1,Vx1,1,2500,False,0,250,1.0,0)
      TP1_Rt = Rf*(TP1_ratio/(1-TP1_ratio))  'calculate resistance
      TP1_temp(i) = 1/(C1+C2*LN(TP1_Rt)+C3*LN(TP1_Rt)^3) - 273.15 - TP1_offset(i)  'calculate temperature with correction for zero offset
      i = i+1
    NextSubScan
    PortSet(1,0)  'Multiplexer OFF

    'Measure some T109's for near Hydra temps, surface and air temp, and deep temps
    Therm109(TempAir,1,5,VX3,0,_60Hz,1,0)
    Therm109(TempHydra(),3,6,VX3,0,_60Hz,1,0)
    Therm109(TempDeep(),3,9,VX2,0,_60Hz,1,0)
    'adjust using offsets
    TempAir = TempAir - TempAir_offset
    TempHydra = TempHydra - TempHydra_offset
    TempDeep = TempDeep - TempDeep_offset

    'Measure the Heat Flux Plate HFP01
    VoltDiff(shf,1,mV25C,7,True,200,250,HFP01_cal/1000,0)

    'Controls for Comm Equipment
    RealTime(rtime)  'get (1) year, (2) month, (3) day of month, (4) hour of day, (5) minutes, (6) seconds, (7) microseconds, (8) day of week (1-7; Sunday = 1), and (9) day of year
    If (CommDay=0 OR rtime(8)=CommDay) AND rtime(4)+rtime(5)/60=CommHour AND Vbat>=12.4 Then
      CommFlag = True
    EndIf
    'Turn Comm ON
    If CommFlag Then
      PortSet(8,1)
      CommTimer = Timer(1,Min,2)  'start and reset timer
      CommFlag = False
    EndIf
    CommTimer = Timer(1,Min,4)  'update timer
    'Turn Comm Off after we reach CommON time
    If NOT CommFlag AND CommTimer>=CommON Then
      PortSet(8,0)
      CommTimer = Timer(1,Min,3)  'stop and reset timer
    EndIf

    'Measure the SR50a snow sensor and hydra probes
    If  TimeIntoInterval(0,30,Min) Then
      SDI12Recorder(Raw_Dist, 7, 0, "M!", 1, 0)
      Distance = Raw_Dist*SQR((TempAir+273.15)/273.15)	'Temperature Correction for distace
      SnowDepth = SR50_height - Distance

      'Hydra II Probes
      SW12(1)  'power on
      Delay (0,5,Sec)
      k = 1
      SubScan(0,uSec,3)
        SDI12Recorder (Hydra(k,1),3,k,"M!",1.0,0) 'read 9 soil parameters
        'reassign things to more useful variables
        Hydra_moisture(k) = Hydra(k,1)
        Hydra_temp(k) = Hydra(k,3)
        Hydra_cond(k) = Hydra(k,5)
        Hydra_real_perm(k) = Hydra(k,6)
        Hydra_img_perm(k) = Hydra(k,7)
        k = k+1
      NextSubScan
      SW12(0)  'power off
    EndIf

    CallTable site_data

  NextScan
EndProg
